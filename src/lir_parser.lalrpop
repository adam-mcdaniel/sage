use ::std::str::FromStr;
use crate::lir::*;
use crate::parse::{CoreProgramParser, StandardProgramParser};

grammar;
Pattern: Pattern = {
    <mut list: (<PatternTerm> "|")+> <last: PatternTerm> => {
        list.push(last);
        Pattern::Alt(list)
    },
    <PatternTerm> => <>,
}

PatternTerm: Pattern = {
    "of" <name: Symbol> <pat: PatternTerm?> => Pattern::Variant(name, pat.map(Box::new)),
    PatternAtom => <>
}

PatternAtom: Pattern = {
    List<"(", Pattern, ",", ")"> => Pattern::Tuple(<>),
    List<("struct" "{"), (<Symbol> "=" <Pattern>), ",", "}"> => Pattern::Struct(<>.into_iter().collect()),
    Symbol => Pattern::Symbol(Mutability::Any, <>),
    // ConstAtom => Pattern::ConstExpr(<>), 
    //<variant: Symbol> "of" <t: TypeTerm> => Pattern::ConstExpr(ConstExpr::Of(t, variant)),
    "true" => Pattern::ConstExpr(ConstExpr::Bool(true)),
    "false" => Pattern::ConstExpr(ConstExpr::Bool(false)),
    "Null" => Pattern::ConstExpr(ConstExpr::Null),
    "None" => Pattern::ConstExpr(ConstExpr::None),
    <IntLit> => Pattern::ConstExpr(ConstExpr::Int(<> as i64)),
    <CharLit> => Pattern::ConstExpr(ConstExpr::Char(<>)),
    "_" => Pattern::Wildcard,
}

Type: Type = {
    "let" <var: Symbol> "=" <t: Type> "in" <ret: Type> => Type::Let(var, Box::new(t), Box::new(ret)),
    "proc" <args: List<"(", Type, ",", ")">> "->" <ret: Type> => {
        Type::Proc(args, Box::new(ret))
    },
    TypeTerm => <>,
}

EnumVariant: (String, Option<Type>) = {
    "of" <Symbol> <Type?> => (<>),
    Symbol => (<>, None),
}

TypeTerm: Type = {
    "(" <Type> ")" => <>,
    Tuple<Type> => Type::Tuple(<>),
    "&" <Type> => Type::Pointer(Mutability::Any, Box::new(<>)),
    "enum" <variants: List<"{", EnumVariant, ",", "}">> => {
        for variant in &variants {
            match variant.1 {
                Some(Type::None) => {}
                Some(_) => return Type::EnumUnion(variants.into_iter().map(|(name, t)| (name, t.unwrap_or(Type::None))).collect()),
                _ => {}
            }
        }
        Type::Enum(variants.into_iter().map(|(name, _)| name).collect())
    },
    "[" <t: Type> "*" <size: ConstExpr> "]" => Type::Array(Box::new(t), Box::new(size)),
    //"enum" <List<"{", (<Symbol> "=" <TypeTerm>), ",", "}">> => Type::EnumUnion(<>.into_iter().collect()),
    //"enum" <Dict<Symbol, Type>> => Type::EnumUnion(<>.into_iter().collect()),
    "union" <Dict<Symbol, Type>> => Type::Union(<>.into_iter().collect()),
    "struct" <Dict<Symbol, Type>> => Type::Struct(<>.into_iter().collect()),
    "unit" <name: Symbol> "=" <t: Type> => Type::Unit(name, Box::new(t)), 
    TypeAtom => <>
}

TypeAtom: Type = {
    Symbol => Type::Symbol(<>),
    "Int" => Type::Int,
    "Float" => Type::Float,
    "Char" => Type::Char,
    "Bool" => Type::Bool,
    "Cell" => Type::Cell,
    "None" => Type::None,
}

ConstExpr: ConstExpr = {
    "proc" <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "->" <ret: Type> "=" <body: Block> => {
        let args = args.into_iter().map(|(name, t)| (name, Mutability::Any, t)).collect();
        ConstExpr::Proc(Procedure::new(None, args, ret, body))
    },
    "proc" <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "=" <body: Block> => {
        let args = args.into_iter().map(|(name, t)| (name, Mutability::Any, t)).collect();
        ConstExpr::Proc(Procedure::new(None, args, Type::None, body))
    },
    "proc" <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "->" <ret: Type> "=" <mut body: CoreAsm> => {
        body.args = args;
        body.ret = ret;
        ConstExpr::CoreBuiltin(body)
    },
    "proc" <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "->" <ret: Type> "=" <mut body: StandardAsm> => {
        body.args = args;
        body.ret = ret;
        ConstExpr::StandardBuiltin(body)
    },
    "typeof" "(" <Expr> ")" => ConstExpr::TypeOf(Box::new(<>)),
    "sizeof" "(" <Type> ")" => ConstExpr::SizeOfType(<>),
    "sizeofexpr" "(" <Expr> ")" => ConstExpr::SizeOfExpr(Box::new(<>)),
    <ConstAtom> => <>,
}

ConstAtom: ConstExpr = {
    // <t: TypeAtom> "of" <variant: Symbol> => ConstExpr::Of(t, variant).into(),
    Symbol => ConstExpr::Symbol(<>),
    IntLit => ConstExpr::Int(<> as i64),
    FloatLit => ConstExpr::Float(<>),
    CharLit => ConstExpr::Char(<>),
    "true" => ConstExpr::Bool(true),
    "false" => ConstExpr::Bool(false),
    "Null" => ConstExpr::Null,
    "None" => ConstExpr::None,
    "(" ")" => ConstExpr::None,
}

pub Expr: Expr = {
    <defs: List<"let", (<Symbol> <(":" <Type>)?> "=" <Logic>), ",", "in">> <e: Expr> => {
        let defs: Vec<_> = defs.into_iter().map(|(name, t, val)| (name, Mutability::Mutable, t, val)).collect();
        Expr::LetVars(defs, Box::new(e))
    },
    <defs: List<"type", (<Symbol> "=" <Type>), ",", "in">> <e: Expr> => {
        Expr::LetTypes(defs, Box::new(e))
    },
    <defs: List<"const", (<Symbol> "=" <ConstExpr>), ",", "in">> <e: Expr> => {
        Expr::LetConsts(defs.into_iter().collect(), Box::new(e))
    },
    "proc" <name: Symbol> <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "->" <ret: Type> "=" <body: Block> "in" <e: Expr> => {
        let args = args.into_iter().map(|(name, t)| (name, Mutability::Any, t)).collect();
        Expr::LetProc(name, Procedure::new(None, args, ret, body), Box::new(e))
    },
    "proc" <name: Symbol> <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "->" <ret: Type> "=" <mut body: CoreAsm> "in" <e: Expr> => {
        body.args = args;
        body.ret = ret;
        Expr::LetConst(name, ConstExpr::CoreBuiltin(body), Box::new(e))
    },
    "proc" <name: Symbol> <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "=" <mut body: CoreAsm> "in" <e: Expr> => {
        body.args = args;
        body.ret = Type::None;
        Expr::LetConst(name, ConstExpr::CoreBuiltin(body), Box::new(e))
    },
    "proc" <name: Symbol> <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "->" <ret: Type> "=" <mut body: StandardAsm> "in" <e: Expr> => {
        body.args = args;
        body.ret = ret;
        Expr::LetConst(name, ConstExpr::StandardBuiltin(body), Box::new(e))
    },
    "proc" <name: Symbol> <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "=" <mut body: StandardAsm> "in" <e: Expr> => {
        body.args = args;
        body.ret = Type::None;
        Expr::LetConst(name, ConstExpr::StandardBuiltin(body), Box::new(e))
    },
    "proc" <name: Symbol> <args: List<"(", (<Symbol> ":" <Type>), ",", ")">> "=" <body: Block> "in" <e: Expr> => {
        let args = args.into_iter().map(|(name, t)| (name, Mutability::Any, t)).collect();
        Expr::LetProc(name, Procedure::new(None, args, Type::None, body), Box::new(e))
    },
    "while" <cond: Expr> <body: Block> => Expr::While(Box::new(cond), Box::new(body)),
    <x: Pointer> "=" <val: Expr> => x.refer(Mutability::Any).deref_mut(val),
    <x: Pointer> "+=" <val: Expr> => x.refer(Mutability::Any).assign_op(Assign::new(Arithmetic::Add), val),
    <x: Pointer> "-=" <val: Expr> => x.refer(Mutability::Any).assign_op(Assign::new(Arithmetic::Subtract), val),
    <x: Pointer> "*=" <val: Expr> => x.refer(Mutability::Any).assign_op(Assign::new(Arithmetic::Multiply), val),
    <x: Pointer> "/=" <val: Expr> => x.refer(Mutability::Any).assign_op(Assign::new(Arithmetic::Divide), val),
    <x: Pointer> "%=" <val: Expr> => x.refer(Mutability::Any).assign_op(Assign::new(Arithmetic::Remainder), val),
    <x: Pointer> "**=" <val: Expr> => x.refer(Mutability::Any).assign_op(Assign::new(Arithmetic::Power), val),
    "return" <Expr> => Expr::Return(Box::new(<>)),
    "debug" <Expr> => Expr::UnaryOp(Put::Debug.clone_box(), Box::new(<>)),
    "put" <Expr> => Expr::UnaryOp(Put::Display.clone_box(), Box::new(<>)),
    "new" <Expr> => Expr::UnaryOp(New.clone_box(), Box::new(<>)),
    Pointer => <>
}

Pointer: Expr = {
    "*" <Pointer> => <>.deref(),
    "&" <Pointer> => <>.refer(Mutability::Any),
    Logic => <>,
}

Logic: Expr = {
    <a: Logic> "or" <b: LogicFactor> => a.or(b),
    LogicFactor => <>,
}
LogicFactor: Expr = {
    <a: LogicFactor> "and" <b: Comparison> => a.and(b),
    "not" <a: Comparison> => a.not(),
    Comparison => <>,
}

Comparison: Expr = {
    <a: Math> "<" <b: Math> => a.lt(b),
    <a: Math> "<=" <b: Math> => a.le(b),
    <a: Math> ">" <b: Math> => a.gt(b),
    <a: Math> ">=" <b: Math> => a.ge(b),
    <a: Math> "==" <b: Math> => a.eq(b),
    <a: Math> "!=" <b: Math> => a.neq(b),
    Math => <>,
}

Math: Expr = {
    <a: Sum> "**" <b: Math> => a.pow(b),
    Sum => <>,
}

Sum: Expr = {
    <a: Sum> "+" <b: Factor> => a.add(b),
    <a: Sum> "-" <b: Factor> => a.sub(b),
    Factor => <>,
}

Factor: Expr = {
    <a: Factor> "*" <b: Term> => a.mul(b),
    <a: Factor> "/" <b: Term> => a.div(b),
    <a: Factor> "%" <b: Term> => a.rem(b),
    Term => <>,
}

Term: Expr = {
    <x: Term> "." <field: IntLit> => x.field(ConstExpr::Int(field as i64)),
    <x: Term> "." <field: Symbol> => x.field(ConstExpr::Symbol(field)),
    <x: Term> "->" <field: IntLit> => x.deref().field(ConstExpr::Int(field as i64)),
    <x: Term> "->" <field: Symbol> => x.deref().field(ConstExpr::Symbol(field)),
    <x: Term> "[" <idx: Expr> "]" => x.idx(idx),
    <x: Term> "as" <t: Type> => x.as_type(t),
    // <x: Term> "as" <t: TypeAtom> "of" <name: Symbol> => {
    //     Expr::EnumUnion(t, name, Box::new(x))
    // },
    <f: Term> <args: List<"(", Expr, ",", ")">> => f.app(args),
    "if" <cond: Atom> <then: Atom> <elif: ("else" "if" <Atom> <Atom>)*> <mut e: ("else" <Atom>)?> => {
        for (c, t) in elif.into_iter().rev() {
            e = Some(Expr::If(Box::new(c), Box::new(t), Box::new(e.unwrap_or(ConstExpr::None.into()))));
        }
        Expr::If(Box::new(cond), Box::new(then), Box::new(e.unwrap_or(ConstExpr::None.into())))
    },
    "if" "let" <pat: Pattern> "=" <expr: Atom> <then: Atom> <elif: ("else" "if" "let" <Pattern> "=" <Atom> <Atom>)*> <mut e: ("else" <Atom>)?> => {
        for (p, c, t) in elif.into_iter().rev() {
            e = Some(Expr::IfLet(p, Box::new(c), Box::new(t), Box::new(e.unwrap_or(ConstExpr::None.into()))));
        }
        Expr::IfLet(pat, Box::new(expr), Box::new(then), Box::new(e.unwrap_or(ConstExpr::None.into())))
    },
    "match" <expr:Atom> <list: NonEmptyList<"{", (<Pattern> "=>" <Expr>), ",", "}">> => {
        Expr::Match(Box::new(expr), list.into_iter().collect())
    },
    // "_tag" <Atom> => <>.unop(Tag),
    // "_data" <Atom> => <>.unop(Data),
    Atom => <>,
}

Atom: Expr = {
    <t: TypeAtom> "of" <name: Symbol> "=" <x: Atom> => {
        Expr::EnumUnion(t, name, Box::new(x))
    },
    <t: TypeAtom> "of" <name: Symbol> "(" ")"=> {
        Expr::ConstExpr(ConstExpr::Of(t, name))
    },
    <StringLit> => Expr::ConstExpr(ConstExpr::Array(<>.chars().map(|c| ConstExpr::Char(c)).collect())),
    Block => <>,
    "(" <Expr> ")" => <>,
    ConstExpr => Expr::ConstExpr(<>),
    Tuple<Expr> => Expr::Tuple(<>),
    Array<Expr> => Expr::Array(<>),
    "struct" "{" <mut list:(<Symbol> "=" <Expr> ",")*> <last: (<Symbol> "=" <Expr>)> "}" => {
        list.push(last);
        Expr::Struct(list.into_iter().collect())
    },
    "union" "{" <mut list:(<Symbol> ":" <Type> ",")*> <last: (<Symbol> ":" <Type> "=" <Expr>)> "}" => {
        list.push((last.0.clone(), last.1));
        let t = Type::Union(list.into_iter().collect());
        Expr::Union(t, last.0, Box::new(last.2))
    },
    "union" "{" <name: Symbol> "=" <e: Expr> "," <t: Type> ".." "}" => {
        Expr::Union(t, name, Box::new(e))
    },
    //"enum" "{" <mut list:(<Symbol> ":" <Type> ",")*> <last: (<Symbol> ":" <Type> "=" <Expr>)> "}" => {
    //    list.push((last.0.clone(), last.1));
    //    let t = Type::Union(list.into_iter().collect());
    //    Expr::EnumUnion(t, last.0, Box::new(last.2))
    //},
    //"enum" "{" <name: Symbol> "=" <e: Expr> "," <t: Type> ".." "}" => {
    //    Expr::EnumUnion(t, name, Box::new(e))
    //},
}

Block: Expr = {
    "{" <mut list: (<Expr> ";")*> <end:Expr?> "}" => {
        list.push(match end {
            Some(val) => val,
            None => ConstExpr::None.into()
        });
        Expr::Many(list)
    }
}

StringLit: String = <s:r#""(\\.|[^"])*""#> => String::from(&s[1..s.len()-1]).replace("\\\"", "\"").replace("\\n", "\n").replace("\\r", "\r").replace("\\t", "\t").replace("\\0", "\0");
CharLit: char = <s:r#"'(\\.|[^'])'"#> => s.replace("\\'", "'").replace("\\n", "\n").replace("\\r", "\r").replace("\\t", "\t").replace("\\0", "\0").chars().nth(1).unwrap() as char;
IntLit: isize = <s:r"-?(0|[1-9][0-9]*)"> => isize::from_str(s).unwrap();
FloatLit: f64 = {
    r"-?([1-9][0-9]*|[0])[.]([0-9]+)?" => <>.to_string().parse::<f64>().unwrap(),
}
Symbol: String = r"[a-zA-Z_][a-zA-Z0-9_]*" => <>.to_string();


Tuple<T>: Vec<T> = {
    "(" <mut list: (<T> ",")+> <end:T?> ")" => {
        match end {
            None => list,
            Some(val) => {
                list.push(val);
                list
            }
        }
    }
}

Array<T>: Vec<T> = List<"[", T, ",", "]"> => <>;
Dict<K, V>: Vec<(K, V)> = List<"{", (<K> ":" <V>), ",", "}"> => <>;

NonEmptyList<Begin, T, Sep, End>: Vec<T> = {
    <first:Begin> <list: (<T> <Sep>)*> <end:(<T> Sep?)> <last:End> => {
        let mut list: Vec<_> = list.iter().map(|(v, s)| v.clone()).collect();
        list.push(end);
        list
    }
}

List<Begin, T, Sep, End>: Vec<T> = {
    <first:Begin> <list: (<T> <Sep>)*> <end:T?> <last:End> => {
        match end {
            None => list.iter().map(|(v, s)| v.clone()).collect(),
            Some(val) => {
                let mut list: Vec<_> = list.iter().map(|(v, s)| v.clone()).collect();
                list.push(val);
                list
            }
        }
    }
}


CoreAsm: CoreBuiltin = {
    <offset: @L> <body: r"core[\t\r\n ]*[{][^}]*[}]"> =>? match CoreProgramParser::new().parse(&body[body.find("{").unwrap()+1..body.len() - 1]) {
        Ok(prog) => Ok(CoreBuiltin {
            name: String::new(),
            args: vec![],
            ret: Type::None,
            body: prog.code,
        }),
        Err(e) => Err(e.map_location(|loc| offset + body.find("{").unwrap() + 1 + loc)),
    },
}

StandardAsm: StandardBuiltin = {
    <offset: @L> <body: r"std[\t\r\n ]*[{][^}]*[}]"> =>? match StandardProgramParser::new().parse(&body[body.find("{").unwrap()+1..body.len() - 1]) {
        Ok(prog) => Ok(StandardBuiltin {
            name: String::new(),
            args: vec![],
            ret: Type::None,
            body: prog.code,
        }),
        Err(e) => Err(e.map_location(|loc| offset + body.find("{").unwrap() + 1 + loc)),
    },
}